buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
}

group = 'com.flurry'
version = '1.1.0'

description = "Proguard/Native mapping Upload"

sourceCompatibility = 1.8
targetCompatibility = 1.8

String bintrayUser = project.hasProperty("bintray.api.user") ? project.property("bintray.api.user") : ""  // Keep out of source control
String bintrayApiKey = project.hasProperty("bintray.api.key") ? project.property("bintray.api.key") : ""   // Keep out of source control

logger.lifecycle("Bintray user $bintrayUser")

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile 'org.json:json:20160810'
    compile 'org.apache.httpcomponents:httpclient:4.5.1'
    compile 'org.apache.commons:commons-compress:1.12'
    compile 'net.sourceforge.argparse4j:argparse4j:0.7.0'
    compile 'org.slf4j:slf4j-simple:1.7.21'

    testCompile 'junit:junit:4.12'
}

publishing {
    publications {
        lib(MavenPublication) {
            from components.java
        }
        cli(MavenPublication) {
            from components.shadow
        }
    }
}

bintray {
    user = "$bintrayUser"
    key = "$bintrayApiKey"
    publications = ['lib', 'cli']
    pkg {
        userOrg = 'yahoo'
        repo = 'maven'
        name = 'flurry-android-proguard-core'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/flurry/upload-clients.git'
        version {
            name = "$project.version"
        }

    }
}
