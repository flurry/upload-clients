buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.14.0"
    }
}

plugins {
    id 'groovy'
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.7'
    id 'com.gradle.plugin-publish' version '0.14.0'
}

group 'com.flurry'
version '3.1.2'

sourceCompatibility = 1.8
targetCompatibility = 1.8

String bintrayUser = project.hasProperty("bintray.api.user") ? project.property("bintray.api.user") : ""  // Keep out of source control
String bintrayApiKey = project.hasProperty("bintray.api.key") ? project.property("bintray.api.key") : ""   // Keep out of source control

logger.lifecycle("Bintray user $bintrayUser")

repositories {
    google()
    jcenter()
    maven {
        url  "http://yahoo.bintray.com/maven"
    }
}

dependencies {
    compileOnly 'com.android.tools.build:gradle:[3.6.4,4.0)'

    compile gradleApi()
    compile localGroovy()
    compile 'com.flurry:proguard-upload:1.1.0'

    // transitive deps aren't being resolved from proguard-upload for some reason...
    compile 'org.json:json:20160810'
    compile 'org.apache.httpcomponents:httpclient:4.5.1'
    compile 'org.apache.commons:commons-compress:1.12'
    compile 'net.sourceforge.argparse4j:argparse4j:0.7.0'
    compile 'org.slf4j:slf4j-simple:1.7.21'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

publishing {
    publications {
        plugin(MavenPublication) {
            from components.java
        }
    }
}

bintray {
    user = "$bintrayUser"
    key = "$bintrayApiKey"
    publications = ['plugin']
    pkg {
        userOrg = 'yahoo'
        repo = 'maven'
        name = 'flurry-android-proguard-plugin'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/flurry/upload-clients.git'
        version {
            name = "$project.version"
        }

    }
}

pluginBundle {
    website = 'https://github.com/flurry/upload-clients'
    vcsUrl = 'https://github.com/flurry/upload-clients'
    description = 'Send ProGuard information to Flurry to allow deobfuscation of received stack traces'
    tags = ['ProGuard', 'Android', 'Flurry']

    plugins {
        proguardPlugin {
            id = 'com.flurry.android.symbols'
            displayName = 'Flurry ProGuard plugin'
        }
    }
}

